
_gitroot='git://github.com/i-rinat/freshplayerplugin.git'
_gitname='freshplayerplugin'

pkgname=freshplayerplugin-git
pkgver=r484.9b30e58
pkgrel=1
pkgdesc="PPAPI-host to NPAPI-plugin adapter"
arch=('i686' 'x86_64')
url="https://github.com/i-rinat/freshplayerplugin"
license=('MIT')
depends=('pango' 'alsa-lib' 'freetype2' 'uriparser' 'chromium-pepper-flash')
makedepends=('git' 'cmake')
provides=('freshplayerplugin')
conflicts=('freshplayerplugin')
source=($_gitroot)
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$_gitname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${_gitname}"
  sed -i 's/opt\/google\/chrome/usr\/lib/' src/np_entry.c
  cmake -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}/${_gitname}"
  install -d "$pkgdir/usr/lib/mozilla/plugins"
  install -Dm644 libfreshwrapper.so "$pkgdir/usr/lib/mozilla/plugins"
}

# vim: ft=sh syn=sh et
